# ******************************************************************************
# Copyright 2017-2021 Intel Corporation
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ******************************************************************************
#
#
# FindNGraph
# ------
#
# This script defines the following variables and imported targets:
#
#   ngraph::ngraph                         - nGraph core target
#   ngraph_FOUND                           - True if the system has the nGraph library
#   NGRAPH_LIBRARIES                       - nGraph libraries
#
# Frontends:
#
#   ngraph::frontend_manager               - nGraph frontend manager
#
#   ngraph_onnx_ngraph_frontend_FOUND      - True if the system has onnx_ngraph_frontend library
#   ngraph::onnx_ngraph_frontend           - ONNX FrontEnd target (optional)
#
#   ngraph_paddlepaddle_frontend_FOUND     - True if the system has PDPD frontend
#   ngraph::paddlepaddle_ngraph_frontend   - nGraph PDPD frontend (optional)
#

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

find_dependency(OpenVINO
                PATHS "${CMAKE_CURRENT_LIST_DIR}"
                      "${CMAKE_CURRENT_LIST_DIR}/ngraph"
                NO_CMAKE_FIND_ROOT_PATH
                NO_DEFAULT_PATH)

# create targets with old names for compatibility
if(TARGET openvino::core AND NOT TARGET ngraph::ngraph)
    add_library(ngraph::ngraph INTERFACE IMPORTED)
    set_target_properties(ngraph::ngraph PROPERTIES
        INTERFACE_LINK_LIBRARIES openvino::core)
endif()

if(TARGET openvino::frontend::manager AND NOT TARGET ngraph::frontend_manager)
    add_library(ngraph::frontend_manager INTERFACE IMPORTED)
    set_target_properties(ngraph::frontend_manager PROPERTIES
        INTERFACE_LINK_LIBRARIES openvino::frontend::manager)
endif()

if(TARGET openvino::frontend::onnx AND NOT TARGET ngraph::onnx_ngraph_frontend)
    add_library(ngraph::onnx_ngraph_frontend INTERFACE IMPORTED)
    set_target_properties(ngraph::onnx_ngraph_frontend PROPERTIES
        INTERFACE_LINK_LIBRARIES openvino::frontend::onnx)
endif()

if(TARGET openvino::frontend::paddlepaddle AND NOT TARGET ngraph::paddlepaddle_ngraph_frontend)
    add_library(ngraph::paddlepaddle_ngraph_frontend INTERFACE IMPORTED)
    set_target_properties(ngraph::paddlepaddle_ngraph_frontend PROPERTIES
        INTERFACE_LINK_LIBRARIES openvino::frontend::paddlepaddle)
endif()

set(ngraph_ngraph_FOUND ON)
set(NGRAPH_LIBRARIES ngraph::ngraph)

set(ngraph_onnx_ngraph_frontend_FOUND ${OpenVINO_Frontend_ONNX_FOUND})
set(ngraph_onnx_importer_FOUND ${OpenVINO_Frontend_ONNX_FOUND})

if(ngraph_onnx_importer_FOUND)
    set(ONNX_IMPORTER_LIBRARIES ngraph::onnx_ngraph_frontend)
    # ngraph::onnx_importer target and variables are deprecated
    # but need to create a dummy target for BW compatibility
    if(NOT TARGET ngraph::onnx_importer)
        add_library(ngraph::onnx_importer INTERFACE IMPORTED)
        set_target_properties(ngraph::onnx_importer PROPERTIES
            INTERFACE_LINK_LIBRARIES ngraph::onnx_ngraph_frontend)
    endif()
endif()

set(ngraph_paddlepaddle_frontend_FOUND ${OpenVINO_Frontend_PaddlePaddle_FOUND})
set(ngraph_ir_frontend_FOUND ${OpenVINO_Frontend_IR_FOUND})

check_required_components(ngraph)
